import org.jooq.codegen.GenerationTool
import org.jooq.meta.jaxb.*

buildscript {
    dependencies {
        classpath group: 'org.jooq', name: 'jooq-meta', version: jooq_version
        classpath group: 'org.jooq', name: 'jooq-meta-extensions', version: jooq_version
        classpath group: 'org.jooq', name: 'jooq-codegen', version: jooq_version
        classpath group: 'org.jooq', name: 'jooq-kotlin', version: jooq_version
        classpath group: 'org.xerial', name: 'sqlite-jdbc', version: '3.39.2.1'
    }
}

plugins {
    id "de.undercouch.download" version "5.3.0"
}

kotlin.sourceSets["main"].kotlin {
    srcDir("src/main/jooq")
}

dependencies {
    api project(":jcdb-api")

    api group: 'io.github.microutils', name: 'kotlin-logging', version: '1.8.3'
    api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    api group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    api group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-cbor', version: '1.3.3'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.39.2.1'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    implementation group: 'org.jooq', name: 'jooq', version: jooq_version

    testImplementation project(':jcdb-testing')
    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'
    testImplementation "org.unittestbot.soot:soot-utbot-fork:4.4.0-FORK-2"

    testImplementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-benchmark-runtime', version: '0.4.4'
}

task downloadIdeaCommunity(type: Download) {
    src intellij_community_url
    dest "idea-community/idea-community.zip"
}

task downloadAndUnzipIdeaCommunity(dependsOn: downloadIdeaCommunity, type: Copy) {
    from zipTree(downloadIdeaCommunity.dest)
    into "idea-community/unzip"
}

benchmark {
    configurations {
        main {
            exclude("JcdbBenchmarks")
            exclude("SootBenchmarks")
        }
        jcdb {
            include("JcdbBenchmarks")
        }
        soot {
            include("SootBenchmarks")
        }
        awaitBackground {
            include("JcdbJvmBackgroundBenchmarks")
            include("JcdbAllClasspathBackgroundBenchmarks")
            include("JcdbIdeaBackgroundBenchmarks")
        }
    }
}

def benchmarkTasks = ["testJcdbBenchmark", "testSootBenchmark"]
tasks.matching { benchmarkTasks.contains(it.name) }.configureEach {
    dependsOn("downloadAndUnzipIdeaCommunity")
}

task generateSqlScheme() {
    String url = "jdbc:sqlite:file:" + database_location
    String driver = "org.sqlite.JDBC"
    GenerationTool.generate(new Configuration()
            .withJdbc(new Jdbc()
                    .withDriver(driver)
                    .withUrl(url)
            )
            .withGenerator(new Generator()
                    .withName("org.jooq.codegen.KotlinGenerator")
                    .withDatabase(new Database())
                    .withGenerate(new Generate()
                            .withDeprecationOnUnknownTypes(Boolean.FALSE)
                    )
                    .withTarget(new Target()
                            .withPackageName("org.utbot.jcdb.impl.storage.jooq")
                            .withDirectory(project.file("src/main/jooq").absolutePath)
                    )
            ))
}